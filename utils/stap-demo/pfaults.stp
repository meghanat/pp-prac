#! /usr/bin/env stap

global fault_entry_time, fault_address, fault_access
global time_offset

probe begin { time_offset = gettimeofday_us() }

probe vm.pagefault {
  t = gettimeofday_us()
  id = tid()
  fault_entry_time[id] = t
  fault_address[id] = address
  fault_access[id] = write_access ? "w" : "r"
}

probe vm.pagefault.return {
  t=gettimeofday_us()
  id = tid()
  if (!(id in fault_entry_time)) next
  e = t - fault_entry_time[id]
  if (vm_fault_contains(fault_type,VM_FAULT_MINOR)) {
    ftype="minor"
  } else if (vm_fault_contains(fault_type,VM_FAULT_MAJOR)) {
    ftype="major"
  } else {
    next #only want to deal with minor and major page faults
  }

  printf("%d:%d:%id:%s:%s:%d\n",
	t - time_offset, id, fault_address[id], fault_access[id], ftype, e)
  #free up memory
  delete fault_entry_time[id]
  delete fault_address[id]
  delete fault_access[id]
}
